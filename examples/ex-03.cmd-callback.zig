const std = @import("std");
const zargs = @import("zargs");
const Command = zargs.Command;
const Arg = zargs.Arg;
const String = @import("ztype").String;

pub fn main() !void {
    const install = Command.new("install")
        .arg(Arg.posArg("name", String))
        .arg(Arg.optArg("count", u32).short('c').default(1));

    const remove = Command.new("remove")
        .arg(Arg.posArg("name", String))
        .arg(Arg.optArg("count", u32).short('c').default(2));

    const _cmd = Command.new("demo").requireSub("action")
        .about("This is a demo showcasing command callbacks.")
        .sub(
            install.callBack(struct {
                fn f(r: *install.Result()) void {
                    r.count *= 2;
                    std.debug.print("[{any}] Installing {s} (count:{d})\n", .{ install.name, r.name, r.count });
                }
            }.f),
        )
        .sub(
            remove.callBack(struct {
                fn f(r: *remove.Result()) void {
                    r.count *= 10;
                    std.debug.print("[{any}] Removing {s} (count:{d})\n", .{ remove.name, r.name, r.count });
                }
            }.f),
        )
        .arg(Arg.opt("verbose", u32).short('v'));
    const cmd = _cmd.callBack(struct {
        fn f(r: *_cmd.Result()) void {
            std.debug.print("[{any}] Success to do {s}\n", .{ _cmd.name, @tagName(r.action) });
        }
    }.f);

    var gpa: std.heap.GeneralPurposeAllocator(.{}) = .init;
    const allocator = gpa.allocator();

    _ = cmd.parse(allocator) catch |e|
        zargs.exitf(e, 1, "\n{s}\n", .{cmd.usageString()});
}
